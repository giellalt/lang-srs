#
# Tsuut'ina morphophonology
#

define vowel [a|á|à|à2|i|í|ì|o|ó|ò|u|ú|ù];
define unmarkedVowel [a|i|o|u];
define highToneVowel [á|í|ó|ú];
define lowToneVowel [à|ì|ò|ù];
define consonant [b|c|d|g|h|j|k|l|ł|m|n|p|s|t|w|x|y|z|ʔ|%'];


#
# Morphophonological adjustments to tone.
#

# The distributive plural dà- has slightly different tonal behaviour compared
# to other low-tone prefixes in Tsuut'ina: before 0-IPFV 1PL isaà-/isiì-, dà-
# combines to form dààsaà-/dààsiì- (i.e., LM -> LL, e.g., dààsaàtsiy "each and
# every one of us is crying", from dà- + isaàtsiy), rather than the expected
# *daasaà-/*daasiì- (i.e., LM -> MM, as is found in all other LM contexts).
# In all other contexts, dà- behaves tonally like any other low-marked prefix.
#
# We model this by representing the low-tone à in distributive plural dà- with
# the multi-character symbol <à2>, then targetting the <i2> moraic /i/ that 
# represents the initial vowel in 0-IPFV 1PL isaà-/isiì-.
define distributivePlural1PLToneSandhi [ à2 "." i2 -> à ì ];

# Define LH -> MH (e.g., shì-á -> shaá, as in shaásʔín "I prepared food, made
# a feast", dà-á -> daá, as in daátłiyaàʔí "each and every one of us saw each
# other") HL -> HM (e.g., ná-ì -> náa, as in náaya "he/she got up"), and 
# LM -> MM (e.g., xà-i -> xaa, as in xaaʔò "s/he took something out", from
# xà- i- DOBJ.INDEF ʔò "handle solid object")
define toneSandhi à -> a, ì -> i, ò -> o, ù -> u || _ highToneVowel,
                                                    highToneVowel _,
                                                    _ unmarkedVowel;

# Certain high-tone prefixes in Tsuut'ina (e.g., tsí- "flee", or í- from 
# historical *uˑ-) spread high tone rightward onto unmarked vowels,
# stopping at the first inflectional chunk (e.g., íts'idiyískid "they were
# asked" comes out as íts'ídíyískid, with high tone spreading from í- onto
# ts'i- and di-, but stopping at yi-).  We mark this spreading high tone
# in lexical entries as an ^H after the vowel (e.g., tsí^H, í^H)
#
# For now, this rule applies to sequences of at most three unmarked syllables
# that appear after a spreading high tone.  If it turns out that there are
# words with a spreading high tone that involve more than three unmarked
# syllables before the inflectional chunk, this rule will need to change.
define hToneSpread [..] -> "^H" || 
    "^H" consonant+ unmarkedVowel+ _ [ \"^H" | "." ];

define hToneSpreadApply a a -> á á, a -> á,
                           i i -> í í, i -> í,
                           o o -> ó ó, o -> ó,
                           u u -> ú ú, u -> ú || _ "^H";

define hToneSpreadCleanup "^H" -> 0;

regex [ hToneSpread .o. 
        hToneSpread .o. 
        hToneSpread .o.
        hToneSpreadApply .o.
        hToneSpreadCleanup ];
define hToneSpreading;

# The ì- transitional (Leer 1996/2005) or inchoative/semelfactive (K. Rice
# 2000) prefix behaves differently from all other tone-bearing prefixes in
# Tsuut'ina, pulling down the tone of neighbouring vowels in inner and middle
# prefixes without adding a mora (e.g., gùja ánìstłàg "I did you (sg.) good",
# from á= ni_ ^L . is làg, with L . is -> ìs; gùja ásilàg "you (sg.) did me
# good", from á= si_ ^L . í làg, with ^L . í -> i).  This prefix does not
# affect the tone of vowels in outer prefixes, but instead behaves phonologī-
# cally like a moraic low-tone /ì/ (e.g., náayisʔoł "I am butchering it
# (progressive)", from ná= ^L . yis ʔoł, not *náyisʔoł or *nayisʔoł).
# 
# In the case of TAMA chunks, this prefix causes both short and level long
# vowels to drop by one level (e.g., í -> i, íí -> ii, etc.), while leaving
# contour tones (specifically, 0-IPFV 1PL aà/iì) unaffected.  In all other
# cases, only one mora is lowered (e.g., nánaàyistsòni "I will see you (pl.)
# again", from ná= naa_ ^L . yis tsòni, where naa ^L -> naà, rather than *nàà).
# To model this, we represent this prefix's tone as ^L, adjusting TAMA chunk
# tones first (i.e., apply the lowering to any vowel sequences immediately
# after the "." boundary marker), then to any adjacent vowel immediately to
# the left of this prefix.
define lToneLowerRight [ "." "^L" á á -> "." a a      .o.
                         "." "^L" á -> "." a          .o.
                         "." "^L" a à -> "." a à      .o.
                         "." "^L" a a -> "." à à      .o.
                         "." "^L" a -> "." à          .o.
                         "." "^L" í í -> "." i i      .o.
                         "." "^L" í -> "." i          .o.
                         "." "^L" i ì -> "." i ì      .o.
                         "." "^L" i i -> "." ì ì      .o.
                         "." "^L" i -> "." ì          .o.
                         "." "^L" ì -> "." ì          .o.
                         "." "^L" i2 -> "." ì ];
define lToneLowerLeft  á -> a, a -> à, í -> i, i -> ì,
                       ó -> o, o -> ò, ú -> u, u -> ù || _ "." "^L";
define lToneCleanup "^L" -> 0;

regex [ lToneLowerRight .o. lToneLowerLeft .o. lToneCleanup ];
define lToneLowering;

# As noted above, the transitional prefix does not pull down the tone of
# neighbouring vowels when those belong to a disjunct prefix.  Instead,
# immediately after a tone-bearing disjunct prefix when there are no following
# vowels whose tones could be lowered, the transitional prefix behaves like
# a moraic /ì/.
#
# (The only exception is the distributive plural dà-, which is a disjunct
# prefix, but after which ^L is not realized [e.g., náasʔoł "you both are
# butchering it", from ná=_^L.asʔoł; vs. nádàsʔoł "each and every one of you
# is butchering it", not *nádààsʔoł, from ná=dà_^L.asʔoł])
define lToneAfterDisjunctBoundary "^L" -> ì || 
                                        "=" "_" "." _ [ consonant | "^P" ];

# Enclitics cause open-syllable, mid-tone verb stems to become high tone.
# Whenever an enclitic is present after a stem, the symbol "^Encl" is
# produced, which allows us to adjust the tone accordingly.
define encliticMidToHighTone a -> á, i -> í, o -> ó, u -> ú || _ "^Encl" .o.
                             "^Encl" -> 0;

# Certain TAMA chunks in ni-PFV and si-PFV paradigms (as well as in morpho-
# logically identical stative forms) are preceded by a floating low tone that
# causes any immediately preceding inner prefix ending in a mid-tone vowel to
# become low (e.g., di . ^P sistł'o -> dìsistł'o "I ran", where the 1SG.SBJ
# si-PFV inner prefix TAMA chunk <sis> is preceded by a floating low tone
# represented in the FST by the symbol <^P>, which causes <di> to become <dì>).
#
# The only vowel that is attested in the morphological environment in which
# this floating low tone appears is /i/, making this process straightforward
# to model phonologically.  In all other environments, the floating low tone
# is not realized.
define perfectiveToneLowering i -> ì || _ "." "^P"  .o.  "^P" -> 0;


#
# Morphophonological adjustments to consonants.
#

# Stems that begin with /l/ and /ł/ combine with 2PL as- either as (a|à)ł or,
# for some speakers, as (a|à)stł (e.g., /as+lí/ "you.PL are" -> [ałí] or
# [astłí], /ídas+łù/ "you.PL are driving" -> [ídałù] or [ídastłù]).  We use
# the symbol 's2' to represent the /s/ in any 2PL inflectional chunks that
# are involved in this process.
define lInitialStemsSbjPl2 s2 [ l | ł ] -> [ ł | s l ] ;

# Phonotactic restriction: in general, /sl/ and /sł/ sequences should be
# resolved as /stł/ (e.g., /is+lí/ "I am" -> [istłí], /ídis+łù/ "I am driving"
# -> [ídistłù]).
define slDissimilation s [ l | ł ] -> s t ł;

# In the si-PFV, verb stems that begin with /l/ dissimilate to /dl/ when
# immediately preceded by an /s/ belonging to a 3SG, 3PL, or 4SG form. As
# the following partial paradigm for "manage to get well" (PFV stem <lít>)
# shows, this process does not appear with 1SG or 2PL forms, which 
#
#   1SG.SBJ  dinásistłít        "I managed to get well" (Onespot-Sapir os07036)
#   2SG.SBJ  dinásílít          "you managed to get well"
#   3SG.SBJ  dinásilít          "he/she/it managed to get well"
#   1PL.SBJ  dinásaàlít         "we managed to get well"
#   2PL.SBJ  dinásałít          "you (pl.) managed to get well"
#   3PL.SBJ  dinágisdlít        "they managed to get well"
#   4SG.SBJ  dináts'isdlít      "someone managed to get well"
#
# We use the symbol 's3' to represent the final /s/ in any si-PFV inflectional
# chunks that are associated with this process.
define lInitialStemsSiPFV s3 l -> s d l;

# Some <y>-initial stems alternate with <sh> when preceded by /s/ (e.g., -yón
# "grow (perfective)", as in nìsishón "I grew up" vs. sìsíyón "you (sg.) grew
# up").  We represent the <y> in these stems in lexical entries as <y2>, then
# define the corresponding phonological alternation here.
define y2InitialStemAfterS [ s "y2" -> s h  .o.  "y2" -> y ];

# Some stems containing the vowel <a> alternate with <o> when preceded by
# <sh> (e.g., -yá(h) "walk, go (human non-pl.)", as in dishó "I will walk, go"
# and dashó "you (2) will walk, go" vs. díyá "you (sg.) will walk, go").  We
# treat this here as a productive alternation with the special character "^A",
# though in actuality, this 'process' is limited to a very small set of 
# lexemes.
define aVowelAlternation a -> o, á -> ó, à -> ò || s h _ "^A" .o. "^A" -> 0 ;

# Some stems ending in /n/ may appear with or without that consonant when it
# occurs word-finally (e.g., sistí ~ sistín "I am sleeping") or before an
# enclitic (e.g., sití-la ~ sitín-la "it was lying there (living being)").
# That same /n/ # is always present when it is not in word-final position
# (e.g., when a suffix is present, e.g,. sistíní dósa "because I am sleeping",
# not *sistíí dósa).
#
# We represent the alternating word-final /n/ found in these stems as <n2> in
# lexical entries, then handle the corresponding phonological alternation here.
define n2FinalStems [ "n2" -> [ 0 | n ] || _ [ .#. | "-" ]  .o.  "n2" -> n ];

# Change szh -> sh (mách'ìguniszhoh -> mách'ìgunisshoh "I don't know") (This
# rule may need to change; some people prefer to spell these forms without the
# second /s/)
define zhDevoicing z -> s || s _ h;

# Change sz -> ss (dìszày -> dìssày "he/she/it went hunting") (This rule may
# need to change; some people prefer to spell these forms without the second
# /s/)
define zDevoicing z -> s || s _;

# Change sgh -> sx (isghàł -> isxàł "I am whipping (something), cracking a
# whip"; cf. isaàghàł "we are whipping (something), cracking a whip")
##define ghDevoicing {gh} -> x || s _;


#
# Morphophonological adjustments to vowel length and quality.
#

# When combined with distributive dà-, 2PL.SBJ as-/às- is realized as dàs-
# (i.e., with a short vowel).
define distributivePluralBefore2PL [ a | à ] -> 0 || à2 "." _ s2  .o.
                                          à2 -> à;

# Outside of TAMA-initial contexts, /i/ assimilates to the height and backness
# of any immediately preceding /a/, /o/, or /u/, but retains its tone and mora.
define iAssimilation í -> á, i -> a, ì -> à || [á|a|à] _ .o.
                     í -> ó, i -> o, ì -> ò || [ó|o|ò] _ .o.
                     í -> ú, i -> u, ì -> ù || [ú|u|ù] _;

# When /i/ appears before an /i/ in a TAMA chunk (e.g., with indefinite object
# i-), then we need to make sure that the first /i/ picks up the tone and
# length of the TAMA chunk.
define iTAMAAssimilation                  í "." i i -> í í   .o.
                          [ í "." [í|i] | i "." í ] -> í     .o.
                                          i "." i   -> i     .o.
                                          i "." i2  -> i2;

# When /u/ appears before an /i/ or /ii/ in a TAMA chunk (e.g., with areal
# gu-), then we need to make sure that the /u/ picks up the tone and length
# from the /i/.
define uTAMAAssimilation [ u "." i i | ú "." ì ì | ù "." í í ]  -> u u  .o.
                         [ u "." í í | ú "." í í | ú "." i i ]  -> ú ú  .o.
                         [ u "." ì ì | ù "." i i | ù "." ì ì ]  -> ù ù  .o.
                         [ u "." i | ú "." ì | ù "." í ]        -> u    .o.
                         [ u "." í | ú "." í | ú "." i ]        -> ú    .o.
                         [ u "." ì | ù "." i | ù "." ì ]        -> ù;

# When /a/ appears before /ii/ or /i/ in a TAMA chunk (e.g., with the inner
# prefix ná- in násʔí "look at"), we need to make sure that the /a/ becomes
# long before /ii/ and picks up the appropriate tone.
define aTAMAAssimilation [ á "." í í | á "." i i | á "." á á ] -> á á  .o.
                         [ á "." í   | á "." i   | á "." á   ] -> á    .o.
                           a "." í                             -> á    .o.
                           a "." i i                           -> a a  .o.
                           a "." i                             -> a    .o.
                           à "." i i                           -> à à  .o.
                           à "." i                             -> à;

# In complex prefix combinations, we can sometimes end up with a sequence
# of three adjacent vowels (e.g., dà- í- isaà-, as in daásaàzi "we are all
# calling him/her/it"). If the last vowel is an /i/, we can safely drop it.
define noThreeVowelSequences i -> 0 || vowel vowel _;

# /u/ before /a/ is realized as a glide, but may still contribute a high tone
# to the following vowel.  Change uá -> wá, ua -> wa, uà -> wà (mid-tone u,
# e.g., gwàgúdìlo "someone is sick" < gu- àgúdìlo), as well as úá -> wá, 
# úa -> wá (high-tone u, e.g., kwásdàł "you (pl.) go in" < kú- asdàł).
# 
# (Phoneticlaly, /wa/ can be realized as [wa] or [ɔa]~[ɔˑ], but this isn't
# represented orthographically).
define uGliding u -> w || _ [á|a|à] .o. ú [a|á] -> w á;

# Change iò -> ò, io -> o, ió -> ó (e.g., mi-oghà -> moghà) and uò -> ò,
# uo -> o, uó -> ó (e.g., gu-oghà -> goghà; both cases mostly apply to oblique
# object markers, where this phonological context typically arises).
define uDeletionBeforeO u -> 0 || _ [ó|o|ò];

# 2PL <as2> doesn't contribute a mora to any preceding prefix, (e.g., dàłí
# "you (pl.) are" < dà- ałí, rather than *dàałí), but it does still cause
# manner assimilation (e.g., dastł'áh "you (pl.) run" < di- astłáh).
define assimilationBefore2PL í -> á, i -> a, ì -> à || _ [á|a|à] s2;
# TODO: what to do with às2-? (depends on treatment of transitional ì-)
define reduce2PLAfterVowel a -> 0 || vowel _ s2;

# Short /i/ deletes before non-/i/ vowels.
define iDeletionBeforeOtherVowels i -> 0 || _ [á|a|à|ó|o|ò|ú|u|ù];


#
# FST-internal symbol mangling.
#

# Some lexical entries include "^L" immediately before the inner-prefix
# boundary marker "." (e.g., "ta=di^L.ʔò"), rather than as part of one of the
# inflectional classes that produce "^L" after that marker (e.g., isi-PFV).
# Shifting the "^L" to always appear after that boundary marker makes several
# of the other tone-related rules defined here more straightforward to apply.
define putLToneAfterInnerPrefixBoundaryMarker "^L" "." -> "." "^L";

# Remove any unmarked, non-tone-bearing vowels (i.e., /i/) from the ends of
# prefixes that immediately precede vowel-initial TAMA chunks (e.g., di .
# is tł'áh -> d . is tł'áh).  This step makes it much easier to deal with
# the morphophonology that happens at the point where the inner prefixes meet
# the TAMA chunks.
define shortenDefaultPrefixes i -> 0 || _ "." ("^L") [á|a|à|í|i|ì|i2];

# Remove any unmarked, non-tone-bearing vowels (i.e., /i/) from the ends of
# oblique prefixes that immediately precede vowel-initial words (e.g., mi#
# à=_.st'a -> m#à=_.st'a -> màst'a "I am like him/her/it")
define shortenObliquePrefixes i -> 0 || _ "#" vowel;

# Turn any instances of <i2> into plain /i/. (We use <i2> in some TAMA chunks
# to indicate an /i/ that maintains its mora when a prefix is joined to it,
# e.g., the first /i/ in isaà-/isiì- 1PL)
define cleanupMoraicI i2 -> i;

# Turn any remaining instances of <s2> and <s3> into plain /s/.
define cleanupS2S3 [ s2 | s3 ] -> s;

# Remove middle, outer, and oblique-prefix morphological boundary markers from
# the FST output.
define removeNonInnerBoundaryMarkers [ "_" | "=" | "#" ] -> 0;

# Remove inner-prefix morphological boundary markers from the FST output.
define removeInnerBoundaryMarker "." -> 0;


#
# Compose all of the morphophonology together.
#

regex [ encliticMidToHighTone
.o. putLToneAfterInnerPrefixBoundaryMarker
.o. shortenDefaultPrefixes
.o. shortenObliquePrefixes
.o. lToneAfterDisjunctBoundary
.o. removeNonInnerBoundaryMarkers
.o. perfectiveToneLowering
.o. lToneLowering
.o. hToneSpreading
.o. distributivePlural1PLToneSandhi
.o. distributivePluralBefore2PL
.o. iAssimilation
.o. iTAMAAssimilation
.o. uTAMAAssimilation
.o. aTAMAAssimilation
.o. removeInnerBoundaryMarker
.o. cleanupMoraicI
.o. noThreeVowelSequences
.o. iAssimilation
.o. uGliding
.o. uDeletionBeforeO
.o. assimilationBefore2PL
.o. reduce2PLAfterVowel
.o. lInitialStemsSbjPl2
.o. lInitialStemsSiPFV
.o. cleanupS2S3
.o. iDeletionBeforeOtherVowels
.o. slDissimilation
.o. y2InitialStemAfterS 
.o. aVowelAlternation
.o. n2FinalStems
.o. zhDevoicing 
.o. zDevoicing
#.o. ghDevoicing
.o. toneSandhi
];
