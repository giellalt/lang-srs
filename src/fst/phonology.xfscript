#
# Tsuut'ina morphophonology
#

define vowel [a|ā|á|à|i|ī|í|ì|o|ō|ó|ò|u|ū|ú|ù];
define unmarkedVowel [a|i|o|u];
define highToneVowel [á|í|ó|ú];
define lowToneVowel [à|ì|ò|ù];
define consonant [b|c|d|g|h|j|k|l|ł|m|n|p|s|t|w|x|y|z|ʔ|%'];


#
# Morphophonological adjustments to tone.
#

# Define LH -> MH (e.g., shì-á -> shaá, as in shaásʔín "I prepared food",
# dà-á -> daá, as in daátłiyaàʔí "each and every one of us saw each other")
# HL -> HM (e.g., ná-ì -> náa, as in náaya "he/she got up"), and 
# LM -> LL (e.g., dà-i -> dàà, as in dààsaàtsiy "each and every one of us
# is crying").
define toneSandhi à -> a, ì -> i, ò -> o, ù -> u || _ highToneVowel .o. 
                  á -> a, í -> i, ó -> o, ú -> u || _ lowToneVowel .o.
                  a -> à, i -> ì, o -> ò, u -> ù || lowToneVowel _;

# Certain high-tone prefixes in Tsuut'ina (e.g., tsí- "flee", or í- from 
# historical *uˑ-) spread high tone rightward onto unmarked syllables,
# stopping at the first inflectional chunk (e.g., íts'idiyískid "they were
# asked" comes out as íts'ídíyískid, with high tone spreading from í- onto
# ts'i- and di-, but stopping at yi-).  We mark this spreading high tone
# in lexical entries as an H after the vowel (e.g., tsíH, íH)
#
# For now, this rule applies to sequences of at most three unmarked syllables
# that appear after a spreading H tone.  If it turns out that there are words
# words with a spreading H tone that involve more than three unmarked
# syllables before the inflectional chunk, this rule will need to change.
define hToneSpreadAfterMid a -> á, i -> í, o -> ó, u -> ú || _ H;
define hToneSpread a a -> á á, a -> á, 
                   i i -> í í, i -> í,
                   o o -> ó ó, o -> ó,
                   u u -> ú ú, u -> ú || 
                        H (consonant)+ _, 
                        H (consonant)+ (unmarkedVowel)+ (consonant)+ _,
                        H (consonant)+ (unmarkedVowel)+ 
                          (consonant)+ (unmarkedVowel)+ (consonant)+ _;
define hToneSpreadCleanup H -> 0;

regex [ hToneSpreadAfterMid .o. hToneSpread .o. hToneSpreadCleanup ];
define hToneSpreading;

# The ì- transitional (Leer 1996/2005) or inchoative/semelfactive (K. Rice
# 2000) prefix behaves differently from all other tone-bearing prefixes in
# Tsuut'ina, pulling down the tone of neighbouring vowels without adding a
# mora (e.g., gùja ánìstłàg "I did you (sg.) good", from á= ni_ L . is làg,
# with L . is -> ìs; gùja ásilàg "you (sg.) did me good", from á= si_ L .
# í làg, with L . í -> i).
# 
# In the case of TAMA chunks, this prefix causes both short and long vowels
# to drop by one level (e.g., í -> i, íí -> ii, etc.).  In all other cases,
# only one mora is lowered (e.g., nánaàyistsòni "I will see you (pl.) again",
# from ná= naa_ L . yis tsòni, where naa L -> naà, rather than *nàà).  To
# model this, we represent this prefix's tone as L, adjusting TAMA chunk tones
# first (i.e., apply the lowering to any vowel sequences immediately after
# the "." boundary marker), then to any adjacent vowel immediately to the
# left of this prefix.
define lToneLowerRight "." L á á -> "." a a, "." L á -> "." a, 
                       "." L a a -> "." à à, "." L a -> "." à, 
                       "." L í í -> "." i i, "." L í -> "." i, 
                       "." L i i -> "." ì ì, "." L i -> "." ì;
define lToneLowerLeft  á -> a, a -> à, í -> i, i -> ì,
                       ó -> o, o -> ò, ú -> u, u -> ù || _ "." L;
define lToneCleanup L -> 0;

regex [ lToneLowerRight .o. lToneLowerLeft .o. lToneCleanup ];
define lToneLowering;


#
# Morphophonological adjustments to consonants.
#

# Stems that begin with /l/ combine with 2PL as- either as (a|à)ł or, for
# some speakers, as (a|à)stł (e.g., /as+lí/ "you.PL are" -> [ałí] or [astłí]).
# We use the symbol 'S' to represent the /s/ in any 2PL inflectional chunks
# that are involved in this process.
define lInitialStemsSbjPl2 S l -> ł ,, S l -> s l ;

# Phonotactic restriction: in general, /sl/ sequences should be resolved as
# /stł/ (e.g., /is+lí/ "I am" -> [istłí]).
define slDissimilation s l -> s t ł;

# Change szh -> sh (mách'ìguniszhoh -> mách'ìgunisshoh "I don't know") (This
# rule may need to change; some people prefer to spell these forms without the
# second /s/)
define zhDevoicing z -> s || s _ h;

# Change sz -> ss (dìszày -> dìssày "he/she/it went hunting") (This rule may
# need to change; some people prefer to spell these forms without the second
# /s/)
define zDevoicing z -> s || s _;

# Change sgh -> sx (isghàł -> isxàł "I am whipping (something), cracking a
# whip"; cf. isaàghàł "we are whipping (something), cracking a whip")
define ghDevoicing gh -> x || s _;


#
# Morphophonological adjustments to vowel length and quality.
#

# Deal with vowel changes before long-vowel-initial TAMA chunks (e.g.,
# idinááʔí "he/she/it saw him/her/itself" < idiná.iiʔí -> idinááʔí)
define longVowelTAMAAssimilation á "." i i -> á á  .o.  á "." í í -> á á  .o.
                                 a "." i i -> a a  .o.  a "." í í -> á á  .o.
                                 à "." i i -> à à  .o.
                                 í "." i i -> í í  .o.  í "." í í -> í í  .o.
                                 i "." i i -> i i  .o.  i "." í í -> í í  .o.
                                 ì "." i i -> ì ì  .o.
                                 ó "." i i -> ó ó  .o.  ó "." í í -> ó ó  .o.
                                 o "." i i -> o o  .o.  o "." í í -> ó ó  .o.
                                 ò "." i i -> ò ò  .o.
                                 ú "." i i -> ú ú  .o.  ú "." í í -> ú ú  .o.
                                 u "." i i -> u u  .o.  u "." í í -> ú ú  .o.
                                 ù "." i i -> ù ù;

define shortVowelTAMAAssimilation á "." i -> á  .o.  á "." í -> á  .o.
                                  a "." i -> a  .o.  a "." í -> á  .o.
                                  à "." i -> à  .o.
                                  í "." i -> í  .o.
                                                     i "." í -> í  .o.
                                  ì "." i -> ì  .o.
                                  ó "." i -> ó  .o.  ó "." í -> ó  .o.
                                  o "." i -> o  .o.  o "." í -> ó  .o.
                                  ò "." i -> ò  .o.
                                  ú "." i -> ú  .o.  ú "." í -> ú  .o.
                                  u "." i -> u  .o.  u "." í -> ú  .o.
                                  ù "." i -> ù;

# In complex prefix combinations, we can sometimes end up with a sequence
# of three adjacent vowels (e.g., dà- í- isaà-, as in daásaàzi "we are all
# calling him/her/it"). If the last vowel is an /i/, we can safely drop it.
define noThreeVowelSequences i -> 0 || vowel vowel _;

# /u/ before /a/ is realized as a glide, but may still contribute a high tone
# to the following vowel.  Change uá -> wá, ua -> wa, uà -> wà (mid-tone u,
# e.g., gwàgúdìlo "someone is sick" < gu- àgúdìlo), as well as úá -> wá, 
# úa -> wá (high-tone u, e.g., kwásdàł "you (pl.) go in" < kú- asdàł).
# 
# (Phoneticlaly, /wa/ can be realized as [wa] or [ɔa]~[ɔˑ], but this isn't
# represented orthographically).
define uGliding u -> w || _ [á|a|à] .o. ú [a|á] -> w á;


# Change iò -> ò, io -> o, ió -> ó (e.g., mi-oghà -> moghà) and uò -> ò,
# uo -> o, uó -> ó (e.g., gu-oghà -> goghà; both cases mostly apply to oblique
# object markers, where this phonological context typically arises).
define uDeletionBeforeO u -> 0 || _ [ó|o|ò];

# 2PL /aS/ doesn't contribute a mora to any preceding prefix, (e.g., dàłí
# "you (pl.) are" < dà- ałí, rather than *dàałí), but it does still cause
# manner assimilation (e.g., dastł'áh "you (pl.) run" < di- astłáh).
define assimilationBefore2PL í -> á, i -> a, ì -> à || _ [á|a|à]+ S;
# TODO: what to do with àS-? (depends on treatment of transitional ì-)
define reduce2PLAfterVowel a -> 0 || vowel _ S;

# Outside of TAMA-initial contexts, /i/ assimilates to the height and backness
# of any immediately preceding /a/ or /u/, but retains its tone and mora.
define iAssimilation í -> á, i -> a, ì -> à || [á|a|à] _ .o.
                     í -> ú, i -> u, ì -> ù || [ú|u|ù] _;

# Short /i/ deletes before non-/i/ vowels.
define iDeletionBeforeOtherVowels i -> 0 || _ [á|a|à|ó|o|ò|ú|u|ù];


#
# FST-internal symbol mangling.
#

# Remove any unmarked, non-tone-bearing vowels (i.e., /i/) from the ends of
# prefixes that immediately precede vowel-initial TAMA chunks (e.g., di .
# is tł'áh -> d . is tł'áh).  This step makes it much easier to deal with
# the morphophonology that happens at the point where the inner prefixes meet
# the TAMA chunks.
define shortenDefaultPrefixes i -> 0 || _ "." (L) [á|a|à|í|i|ì];

# Turn any remaining instances of <S> into plain /s/.
define cleanup2PL S -> s;

# Remove morphological boundary markers from the FST output.
define removeBoundaryMarkers "." -> 0;


#
# Compose all of the morphophonology together.
#

regex [ shortenDefaultPrefixes
.o. hToneSpreading
.o. lToneLowering
.o. longVowelTAMAAssimilation
#.o. shortVowelTAMAAssimilation
.o. removeBoundaryMarkers
.o. uGliding
.o. uDeletionBeforeO
.o. assimilationBefore2PL
.o. reduce2PLAfterVowel
.o. lInitialStemsSbjPl2
.o. cleanup2PL
.o. iAssimilation
.o. iDeletionBeforeOtherVowels
.o. noThreeVowelSequences
.o. slDissimilation
.o. zhDevoicing 
.o. zDevoicing
.o. ghDevoicing
.o. toneSandhi
] ;
