#
# Tsuut'ina morphophonology
#

define vowel [a|ā|á|à|i|ī|í|ì|o|ō|ó|ò|u|ū|ú|ù];
define unmarkedVowel [a|i|o|u];
define highToneVowel [á|í|ó|ú];
define lowToneVowel [à|ì|ò|ù];
define consonant [b|c|d|g|h|j|k|l|ł|m|n|p|s|t|w|x|y|z|ʔ|%'];


#
# Morphophonological adjustments to tone.
#

# Define LH -> MH (e.g., shì-á -> shaá, as in shaásʔín "I prepared food, made
# a feast", dà-á -> daá, as in daátłiyaàʔí "each and every one of us saw each
# other") HL -> HM (e.g., ná-ì -> náa, as in náaya "he/she got up"), and 
# LM -> LL (e.g., dà-i -> dàà, as in dààsaàtsiy "each and every one of us
# is crying").
define toneSandhi à -> a, ì -> i, ò -> o, ù -> u || _ highToneVowel .o. 
                  á -> a, í -> i, ó -> o, ú -> u || _ lowToneVowel .o.
                  a -> à, i -> ì, o -> ò, u -> ù || lowToneVowel _;

# Certain high-tone prefixes in Tsuut'ina (e.g., tsí- "flee", or í- from 
# historical *uˑ-) spread high tone rightward onto unmarked syllables,
# stopping at the first inflectional chunk (e.g., íts'idiyískid "they were
# asked" comes out as íts'ídíyískid, with high tone spreading from í- onto
# ts'i- and di-, but stopping at yi-).  We mark this spreading high tone
# in lexical entries as an ^H after the vowel (e.g., tsí^H, í^H)
#
# For now, this rule applies to sequences of at most three unmarked syllables
# that appear after a spreading high tone.  If it turns out that there are
# words with a spreading high tone that involve more than three unmarked
# syllables before the inflectional chunk, this rule will need to change.
define hToneSpreadAfterMid a -> á, i -> í, o -> ó, u -> ú || _ "^H";
define hToneSpread a a -> á á, a -> á, 
                   i i -> í í, i -> í,
                   o o -> ó ó, o -> ó,
                   u u -> ú ú, u -> ú || 
                        "^H" (consonant)+ _, 
                        "^H" (consonant)+ (unmarkedVowel)+ (consonant)+ _,
                        "^H" (consonant)+ (unmarkedVowel)+ 
                          (consonant)+ (unmarkedVowel)+ (consonant)+ _;
define hToneSpreadCleanup "^H" -> 0;

regex [ hToneSpreadAfterMid .o. hToneSpread .o. hToneSpreadCleanup ];
define hToneSpreading;

# The ì- transitional (Leer 1996/2005) or inchoative/semelfactive (K. Rice
# 2000) prefix behaves differently from all other tone-bearing prefixes in
# Tsuut'ina, pulling down the tone of neighbouring vowels without adding a
# mora (e.g., gùja ánìstłàg "I did you (sg.) good", from á= ni_ ^L . is làg,
# with L . is -> ìs; gùja ásilàg "you (sg.) did me good", from á= si_ ^L .
# í làg, with ^L . í -> i).
# 
# In the case of TAMA chunks, this prefix causes both short and long vowels
# to drop by one level (e.g., í -> i, íí -> ii, etc.).  In all other cases,
# only one mora is lowered (e.g., nánaàyistsòni "I will see you (pl.) again",
# from ná= naa_ ^L . yis tsòni, where naa ^L -> naà, rather than *nàà).  To
# model this, we represent this prefix's tone as ^L, adjusting TAMA chunk tones
# first (i.e., apply the lowering to any vowel sequences immediately after
# the "." boundary marker), then to any adjacent vowel immediately to the
# left of this prefix.
define lToneLowerRight "." "^L" á á -> "." a a, "." "^L" á -> "." a, 
                       "." "^L" a a -> "." à à, "." "^L" a -> "." à, 
                       "." "^L" í í -> "." i i, "." "^L" í -> "." i, 
                       "." "^L" i i -> "." ì ì, "." "^L" i -> "." ì;
define lToneLowerLeft  á -> a, a -> à, í -> i, i -> ì,
                       ó -> o, o -> ò, ú -> u, u -> ù || _ "." "^L";
define lToneCleanup "^L" -> 0;

regex [ lToneLowerRight .o. lToneLowerLeft .o. lToneCleanup ];
define lToneLowering;


#
# Morphophonological adjustments to consonants.
#

# Stems that begin with /l/ combine with 2PL as- either as (a|à)ł or, for
# some speakers, as (a|à)stł (e.g., /as+lí/ "you.PL are" -> [ałí] or [astłí]).
# We use the symbol 's2' to represent the /s/ in any 2PL inflectional chunks
# that are involved in this process.
define lInitialStemsSbjPl2 s2 l -> ł ,, s2 l -> s l ;

# Phonotactic restriction: in general, /sl/ sequences should be resolved as
# /stł/ (e.g., /is+lí/ "I am" -> [istłí]).
define slDissimilation s l -> s t ł;

# Some <y>-initial stems alternate with <sh> when preceded by /s/ (e.g., -yón
# "grow (perfective)", as in nìsishón "I grew up" vs. sìsíyón "you (sg.) grew
# up").  We represent the <y> in these stems in lexical entries as <y2>, then
# define the corresponding phonological alternation here.
define y2InitialStemAfterS [ s y2 -> s h  .o.  y2 -> y ];

# Change szh -> sh (mách'ìguniszhoh -> mách'ìgunisshoh "I don't know") (This
# rule may need to change; some people prefer to spell these forms without the
# second /s/)
define zhDevoicing z -> s || s _ h;

# Change sz -> ss (dìszày -> dìssày "he/she/it went hunting") (This rule may
# need to change; some people prefer to spell these forms without the second
# /s/)
define zDevoicing z -> s || s _;

# Change sgh -> sx (isghàł -> isxàł "I am whipping (something), cracking a
# whip"; cf. isaàghàł "we are whipping (something), cracking a whip")
define ghDevoicing gh -> x || s _;


#
# Morphophonological adjustments to vowel length and quality.
#

# When /u/ appears before an /i/ in a TAMA chunk (e.g., with areal gu-), then
# we need to make sure that the /u/ picks up the tone from the /i/.
define uTAMAAssimilation [ u "." i | ú "." ì | ù "." í ] -> u  .o.
                         [ u "." í | ú "." í | ú "." i ] -> ú  .o.
                         [ u "." ì | ù "." i | ù "." ì ] -> ù;

# When /á/ appears before /ii/ or /i/ in a TAMA chunk (e.g., with the inner
# prefix ná- in násʔí "look at"), we need to make sure that the /á/ becomes
# long before /ii/ and picks up the appropriate tone.
#
# TODO: It would be good to check the cases before /ìì/ and /ì/, which are
# best guesses, based on ì- prefix phonology elsewhere.  It's not clear if
# these sequences ever appear: inner-prefix /á/ is rare as hen's teeth, and
# finding a verb with both inner-prefix /á/ and ì- transitional (= ^L in the
# FST) might be a tall order...
define aTAMAAssimilation [ á "." í í | á "." i i | á "." á á ] -> á á  .o.
                         [ á "." í   | á "." i   | á "." á   ] -> á    .o.
                           á "." ì ì -> a a                            .o.
                           á "." ì   -> a;

# In complex prefix combinations, we can sometimes end up with a sequence
# of three adjacent vowels (e.g., dà- í- isaà-, as in daásaàzi "we are all
# calling him/her/it"). If the last vowel is an /i/, we can safely drop it.
define noThreeVowelSequences i -> 0 || vowel vowel _;

# /u/ before /a/ is realized as a glide, but may still contribute a high tone
# to the following vowel.  Change uá -> wá, ua -> wa, uà -> wà (mid-tone u,
# e.g., gwàgúdìlo "someone is sick" < gu- àgúdìlo), as well as úá -> wá, 
# úa -> wá (high-tone u, e.g., kwásdàł "you (pl.) go in" < kú- asdàł).
# 
# (Phoneticlaly, /wa/ can be realized as [wa] or [ɔa]~[ɔˑ], but this isn't
# represented orthographically).
define uGliding u -> w || _ [á|a|à] .o. ú [a|á] -> w á;

# Change iò -> ò, io -> o, ió -> ó (e.g., mi-oghà -> moghà) and uò -> ò,
# uo -> o, uó -> ó (e.g., gu-oghà -> goghà; both cases mostly apply to oblique
# object markers, where this phonological context typically arises).
define uDeletionBeforeO u -> 0 || _ [ó|o|ò];

# 2PL <as2> doesn't contribute a mora to any preceding prefix, (e.g., dàłí
# "you (pl.) are" < dà- ałí, rather than *dàałí), but it does still cause
# manner assimilation (e.g., dastł'áh "you (pl.) run" < di- astłáh).
define assimilationBefore2PL í -> á, i -> a, ì -> à || _ [á|a|à]+ s2;
# TODO: what to do with às2-? (depends on treatment of transitional ì-)
define reduce2PLAfterVowel a -> 0 || vowel _ s2;

# Outside of TAMA-initial contexts, /i/ assimilates to the height and backness
# of any immediately preceding /a/ or /u/, but retains its tone and mora.
define iAssimilation í -> á, i -> a, ì -> à || [á|a|à] _ .o.
                     í -> ú, i -> u, ì -> ù || [ú|u|ù] _;

# Short /i/ deletes before non-/i/ vowels.
define iDeletionBeforeOtherVowels i -> 0 || _ [á|a|à|ó|o|ò|ú|u|ù];


#
# FST-internal symbol mangling.
#

# Remove any unmarked, non-tone-bearing vowels (i.e., /i/) from the ends of
# prefixes that immediately precede vowel-initial TAMA chunks (e.g., di .
# is tł'áh -> d . is tł'áh).  This step makes it much easier to deal with
# the morphophonology that happens at the point where the inner prefixes meet
# the TAMA chunks.
define shortenDefaultPrefixes i -> 0 || _ "." ("^L") [á|a|à|í|i|ì];

# Turn any instances of <i2> into plain /i/. (We use <i2> in some TAMA chunks
# to indicate an /i/ that maintains its mora when a prefix is joined to it,
# e.g., the first /i/ in isaà-/isiì- 1PL)
define cleanupMoraicI i2 -> i;

# Turn any remaining instances of <s2> into plain /s/.
define cleanup2PL s2 -> s;

# Remove morphological boundary markers from the FST output.
define removeBoundaryMarkers "." -> 0;


#
# Compose all of the morphophonology together.
#

regex [ shortenDefaultPrefixes
.o. hToneSpreading
.o. lToneLowering
.o. uTAMAAssimilation
.o. aTAMAAssimilation
.o. cleanupMoraicI
.o. removeBoundaryMarkers
.o. uGliding
.o. uDeletionBeforeO
.o. assimilationBefore2PL
.o. reduce2PLAfterVowel
.o. lInitialStemsSbjPl2
.o. cleanup2PL
.o. iAssimilation
.o. iDeletionBeforeOtherVowels
.o. noThreeVowelSequences
.o. slDissimilation
.o. y2InitialStemAfterS 
.o. zhDevoicing 
.o. zDevoicing
.o. ghDevoicing
.o. toneSandhi
] ;
