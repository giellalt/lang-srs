Multichar_Symbols @D.GI@ @D.OBJECTPERSON@ @D.PREFIX@ @D.SUBJECTPERSON.1@ @D.SUBJECTPERSON.2@ @D.SUBJECTPERSON.3@ @D.TAMA.si@ @D.VALENCE.TRANSITIVE@ @P.DISTRIBUTIVE.OFF@ @P.DISTRIBUTIVE.ON@ @P.GI.ON@ @P.PREFIX.MIDDLE@ @R.DISTRIBUTIVE.OFF@ @R.DISTRIBUTIVE.ON@ @R.GI@ @R.OBJECTNUMBER.PL@ @R.OBJECTNUMBER.SG@ @R.OBJECTPERSON.1@ @R.OBJECTPERSON.2@ @R.OBJECTPERSON.3@ @R.OBJECTPERSON.4@ @R.OBJECTPERSON.RECIP@ @R.OBJECTPERSON.REFL@ @R.OUTERSUBJECT.ON@ @R.PREFIX.INNER@ @R.PREFIX.MIDDLE@ @R.PREFIX.OUTER@ @R.SUBJECTNUMBER.PL@ @R.SUBJECTNUMBER.SG@ @R.SUBJECTPERSON.3@ @R.SUBJECTPERSON.4@ @R.TAMA.si@ @U.ASPECT.IPFV@ @U.ASPECT.PFV@ @U.ASPECT.POT@ @U.ASPECT.PROG@ @U.DIRECTOBJECT.GIVEN@ @U.DIRECTOBJECT.EMPHATIC@ @U.DIRECTOBJECT.NONE@ @U.OBJECTNUMBER.PL@ @U.OBJECTNUMBER.SG@ @U.OBJECTPERSON.1@ @U.OBJECTPERSON.2@ @U.OBJECTPERSON.3@ @U.OBJECTPERSON.4@ @U.OBJECTPERSON.RECIP@ @U.OBJECTPERSON.REFL@ @U.OBJECTPERSON.INDEF@ @U.OBLIQUENUMBER.PL@ @U.OBLIQUENUMBER.SG@ @U.OBLIQUEPERSON.1@ @U.OBLIQUEPERSON.2@ @U.OBLIQUEPERSON.3@ @U.OBLIQUEPERSON.4@ @U.OBLIQUEPERSON.RECIP@ @U.OBLIQUEPERSON.REFL@ @U.OUTERSUBJECT.ON@ @U.PREFIX.OUTER@ @U.SUBJECTNUMBER.PL@ @U.SUBJECTNUMBER.SG@ @U.SUBJECTPERSON.1@ @U.SUBJECTPERSON.2@ @U.SUBJECTPERSON.3@ @U.SUBJECTPERSON.4@ @U.TAMA.%0@ @U.TAMA.i@ @U.TAMA.isi@ @U.TAMA.ni@ @U.TAMA.si@ @U.TAMA.yi-a@ @U.TAMA.yi-y@ @U.TAMA.yi@ @U.VALENCE.DITRANSITIVE@ @U.VALENCE.OO-EXPERIENCER@ @U.VALENCE.IMPERSONAL@ @U.VALENCE.INTRANSITIVE@ @U.VALENCE.OBLIQUEOBJECT@ @U.VALENCE.TRANSITIONAL@ @U.VALENCE.TRANSITIVE@ @P.CONATIVE.ON@ @D.CONATIVE@ @R.CONATIVE.ON@ @D.PREFIX.MIDDLE@ @P.PREFIX.OUTER@ @P.CONATIVE.OFF@ @R.CONATIVE.OFF@ @U.VV.I@ @U.VV.%0@ @U.OBJECTPERSON.AREAL@ @U.OBLIQUEPERSON.AREAL@ @U.VALENCE.DO-EXPERIENCER@ @P.PREFIX.INNER@ ^H


LEXICON Root

@U.VALENCE.INTRANSITIVE@                              IntransitiveMarkers;
@U.VALENCE.OBLIQUEOBJECT@                             IntransitiveMarkers;
@U.VALENCE.TRANSITIVE@                                TransitiveMarkers;
@U.VALENCE.DITRANSITIVE@                              TransitiveMarkers;
@U.VALENCE.IMPERSONAL@                                Only3SGSubject;
@U.VALENCE.DO-EXPERIENCER@                            DirectSubjectAffixes;
@U.VALENCE.OO-EXPERIENCER@                            #;
@U.VALENCE.TRANSITIONAL@                              TransitiveMarkers;


LEXICON IntransitiveMarkers

! With intransitive and oblique object verbs, the only two affixes that can
! appear in this region of the verb are the two 'outer' subject markers gi-
! 3PL (if it hasn't occurred earlier in the verb, which can happen with some
! oblique object forms) and ts'i- 4SG.

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@                       Filter-SBJ-IO;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@                       Filter-SBJ-IO;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                       Filter-SBJ-IO;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@                       Filter-SBJ-IO;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@                       Filter-SBJ-IO;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@R.GI@                 Filter-SBJ-IO;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@gi
    Filter-SBJ-IO;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@@P.PREFIX.MIDDLE@ts'i
    Filter-SBJ-IO;


LEXICON TransitiveMarkers

! With transitive verbs, three different kinds of prefixes can appear in this
! part of the verb, in the following order:
!
!   1. Direct object markers (which may take different forms, depending on
!      the person and number of the subject; or, for third-person objects,
!      may be omitted when a coreferential full nominal is present and/or
!      when the subject is not third-person)
!
!   2. The 'conative' (K. Rice 2000) or 'half-transitive' (Leer 1996, 2005)
!      prefix /í-/, which appears between the direct object markers and any
!      following outer subject prefix.
!
!   3. Outer subject markers, namely 3PL.SBJ gi- "they" and 4SG.SBJ ts'i-
!      "someone (unseen, unspecified, or holy)".

! 1SG direct object (e.g., siyíʔí "you (sg.) saw me")
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@si        Conative;

! 2SG direct object (e.g., niyisʔí "I saw you")
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@ni        Conative;

! Handle 3SG direct objects in their own lexicon.
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.SG@                           3SG-DO;

! 1PL direct object (e.g., nihiyíʔí / nahiyíʔí "you (sg.) saw us")
!
! TODO: Confirm that the same naa- variant as 2PL below exists for 1PL, then
! implement here if needed! (talk to Bruce)
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nihi      Conative;
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nahi      Conative;

! 2PL direct object (e.g., nihiyisʔí / nahiyisʔí "I saw you (pl.)")
!
! TODO: There is a variant of these forms, naa-, that appears after an outer
! prefix, e.g., nánaàyistsòni "I will see you all again".  Add and test this!
! (talk to Bruce re: distribution of naa-)
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nihi      Conative;
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nahi      Conative;

! Handle 3PL direct objects in their own lexicon.
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.PL@                           3PL-DO;

! 4SG (unspecified) direct object (e.g., guyisʔí "I saw someone").
@U.OBJECTPERSON.4@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@gu        Conative;

! Areal direct object (e.g., guyisʔí "I saw it (a place or space)").
! This prefix is homophonous with 4SG.DO gu- (and with 3>3PL.DO gu-), but
! syntactically and semantically distinct; cf. TLQ-20210521.
!
! TODO: We treat areal objects as if they were singular, but, like 4SG 
! subjects and direct objects, they could likely be seen as being unmarked
! for number.  
@U.OBJECTPERSON.AREAL@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@gu    Conative;

! Handle reflexives in their own lexicon.
@U.OBJECTPERSON.REFL@@P.PREFIX.MIDDLE@                          Reflexive-DO;

! Handle reciprocals in their own lexicon.
@U.OBJECTPERSON.RECIP@@P.PREFIX.MIDDLE@                         Reciprocal-DO;

! The indefinite object marker i- "something" in Tsuut'ina rides the fence
! between inflectional and derivational affixation.  On the one hand, i-
! doesn't co-occur with the other direct object markers seen above, and has a
! regular, direct-object-related meaning whenever present (e.g., iyisʔí(n) "I
! saw *something*" vs. yisʔí(n) "I saw *him/her/it*").  On the other hand, i-
! cannot occur with all transitive verbs -- whether or not a given transitive
! verb can appear with i- is essentially lexeme-specific information, and
! would likely be best treated as derivational.
!
! It would be nice to have i- represented in some way in the analyses produced
! and accepted by this model, even while modelling the presence or absence of
! i- as part of the lexical entry's definition (e.g., "kú=i_lo").  We therefore
! define the person "INDEF" here **without an accompanying affix form** (unlike
! si-, ni-, etc. above).  This allows us to recognize i- when it's present in
! the analysis of this lexeme, without generating the affix form here.
! (Transitive lexemes containing i- get marked as requiring @R.OBJECTPERSON.
! INDEF@, which prevents all other direct object markers from occurring. All
! other transitive lexemes without i- use @D.OBJECTPERSON.INDEF@ to reject this
! affix)
@U.OBJECTPERSON.INDEF@@U.OBJECTNUMBER.SG@                      Conative;


LEXICON 3SG-DO
! 3SG direct objects have different forms with non-3, 3SG, and 3PL subjects.
@D.SUBJECTPERSON.3@                                            3SG-DO-Non3-Sbj;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                        3SG-DO-3SG-Sbj;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@                        3SG-DO-3PL-Sbj;

LEXICON 3SG-DO-Non3-Sbj
! 3SG direct object with non-3 subject (e.g., ístłí yisʔí "I saw a horse",
! yisʔí "I saw him/her/it", miyisʔí "I saw him/her/it" (emphatic); cf.
! TLQ-20210510)
@U.DIRECTOBJECT.GIVEN@                                          Conative;
@U.DIRECTOBJECT.NONE@                                           Conative;
@U.DIRECTOBJECT.EMPHATIC@@P.PREFIX.MIDDLE@mi                    Conative;

LEXICON 3SG-DO-3SG-Sbj
! 3SG direct object with 3SG subject (e.g., ístłí yíʔí "s/he saw a horse",
! yáʔí "s/he saw it (< yi- + yíʔí))
@U.DIRECTOBJECT.GIVEN@                                          Conative;
@U.DIRECTOBJECT.NONE@@P.PREFIX.MIDDLE@yi                        Conative;

LEXICON 3SG-DO-3PL-Sbj
! 3SG direct object with 3PL subject (e.g., ístłí gáʔí "they saw a horse"
! (< gi- + yíʔí), giyáʔí "they saw it (< giyi- + yíʔí))
!
! Since gi- could already have appeared in front of a prefix to the left of
! here, we need two separate paths here: one for when this is the first
! occurrence of gi- (@D.GI@, followed by @P.GI.ON@), and another for the case
! when gi- has already been encountered/expressed (@R.GI@).
@U.DIRECTOBJECT.GIVEN@                                          Conative;
@U.DIRECTOBJECT.NONE@@R.GI@@P.PREFIX.MIDDLE@yi                  Conative;
@U.DIRECTOBJECT.NONE@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@giyi       Conative;


LEXICON 3PL-DO
! 3PL direct objects have different forms with non-3 and 3 subjects.
@D.SUBJECTPERSON.3@                                            3PL-DO-Non3-Sbj;
@U.SUBJECTPERSON.3@                                            3PL-DO-3-Sbj;

LEXICON 3PL-DO-Non3-Sbj
! 3PL direct object with non-3 subject (e.g., gimilích'àká yisʔí "I saw their
! horses", gimiyisʔí "I saw them")
@U.DIRECTOBJECT.GIVEN@                                          Conative;
@U.DIRECTOBJECT.NONE@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@gimi       Conative;
! TODO: Check this form with Bruce -- "(you sg.) feed them to him/her/it"
! (moghà giminíschúd or gimoghà miníschúd?) or "someone is feeding them to it"
! (moghà gimits'íschúd or gimoghà mits'íschúd?).  Looks like this should be
! moghà, but it would be best to double-check this.
!!@U.DIRECTOBJECT.NONE@@R.GI@@P.PREFIX.MIDDLE@mi                  Conative;
@U.DIRECTOBJECT.NONE@@R.GI@                                     Conative;

LEXICON 3PL-DO-3-Sbj
! 3PL direct object with 3SG subject (e.g., giyilích'àká yíʔí "s/he saw their
! horses", gwáʔí "s/he saw them"/gùʔás "s/he will kick them" [TLQ-20210514])
!
! 3PL direct object with 3PL subject (e.g., gulích'àká gáʔí "they saw their
! horses", gugáʔí "they saw them"/gugìʔás "they will kick them [TLQ-20210514])
@U.DIRECTOBJECT.GIVEN@                                          Conative;
@U.DIRECTOBJECT.NONE@@P.PREFIX.MIDDLE@gu                        Conative;


LEXICON Reflexive-DO
! Reflexive direct objects are accompanied by a change in voice-valence markers
! in Tsuut'ina.  Specifically, VV s- becomes i-, e.g., sinánisʔí "you (sg.)
! look at me!" (nis-, 0s-IPFV) vs. idinániiʔí "you (sg.) look at yourself")
! (nii-, 0i-IPFV).
!
! (Note that, although yi-a and yi-yi perfectives developed historically out of
! differences in voice-valence markers (*d- and *l- --> yi-a, *0- and *ł -->
! yi-yi), these synchronic shifts in voice-valence markers with reflexive
! objects don't cause yi-y perfectives to become yi-a forms in modern-day
! Tsuut'ina (e.g., the reflexive form of mídíyiskid "I asked him/her/it"
! [yis-yi-PFV, *ł-VV] doesn't become yii-a-PFV in a reflexive form like "I
! asked myself").)
!
! Unlike some other Dene languages, this pattern appears only with reflexive
! objects in Tsuut'ina, not with reciprocals; cf. siginásʔín "they are looking
! at me" (0s-IPFV) vs. átłiginásʔín "they are looking at each other" (0s-IPFV)
! vs. igidinááʔín "they are looking at themselves" (0i-IPFV). [TLQ-20210510]
@U.VV.I@                                                    Reflexive-DO-Num;
@U.VV.%0@                                                   Reflexive-DO-Num;

LEXICON Reflexive-DO-Num
! Reflexive objects have the same number as their (coreferential) subjects.
@U.SUBJECTNUMBER.SG@@U.OBJECTNUMBER.SG@                     Reflexive-DO-AFF;
@U.SUBJECTNUMBER.PL@@U.OBJECTNUMBER.PL@                     Reflexive-DO-AFF;

LEXICON Reflexive-DO-AFF

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@idi                  Conative;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@idi                  Conative;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@idi                  Conative;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@idi                  Conative;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@idi                  Conative;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@R.GI@idi            Conative;
! Free variation between 3PL.REFL igidi- and idigi-, both used interchangeably
! [TLQ-20210510]
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@D.GI@@P.GI.ON@igidi Conative;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@D.GI@@P.GI.ON@idigi Conative;
! TODO: Check 4SG reflexive objects with Bruce (variation between its'idi-
! and idits'i-, paralleling 3SG variation?)
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@idi                  Conative;


LEXICON Reciprocal-DO

! Reciprocal objects have the same number as their subjects.
@U.SUBJECTNUMBER.SG@@U.OBJECTNUMBER.SG@                     Reciprocal-DO-AFF;
@U.SUBJECTNUMBER.PL@@U.OBJECTNUMBER.PL@                     Reciprocal-DO-AFF;

LEXICON Reciprocal-DO-AFF
! Reciprocal forms only exist with plural and fourth-person subjects, e.g.,
! átłiyasʔí "you (pl.) saw each other", ástłits'áʔí "someone/people saw each
! other".
@U.SUBJECTNUMBER.PL@átłi                                    Conative;
@U.SUBJECTNUMBER.PL@ástłi                                   Conative;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@átłi                 Conative;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@ástłi                Conative;


LEXICON Conative
! Some verbs have a lexically-specified prefix í- ("half-transitive" according
! to Leer 1996/2005, "conative" in K. Rice 2000 and others) that appears
! between the direct object markers and the outer subject markers in Tsuut'ina.
! This prefix contributes a high tone to the final syllable of direct object
! markers in these verbs (e.g., giyízi "they will call him/her/it", from giyi-
! í- zi), and also spreads rightward onto any tonally unspecified syllables up
! to (but not including) the TAMA chunk (e.g., míts'ízi "someone will call
! him/her/it", from mi- í- ts'i- zi).
!
! We mark the presence of this conative/half-transitive prefix using the flag
! CONATIVE, which is set as part of the lexical entry for each verb that
! contains this prefix.  Here, we define two paths: one which sets this flag
! (and eventually matches with @R.CONATIVE.ON@ in the lexicon) and adds the
! prefix, and one that doesn't (and eventually rules out conative-transitive
! forms from being produced/accepted for verbs that don't have this prefix
! specified in the lexicon).
!
! The conative prefix is phonologically unusual in that it appears in middle-
! prefix territory, but appears with outer-prefix TAMA chunk variants: cf.
! conative mínizi "you (sg.) are calling him/her/it" (with full 2SG ni-,
! 0-IPFV) versus non-conative tásítsíh "you (sg.) paint me" (with reduced
! 2SG í-, also 0-IPFV).  We handle this by flipping any @P.PREFIX.MIDDLE@
! flags to @P.PREFIX.OUTER@, which causes the correct TAMA chunk to be
! selected.

@P.CONATIVE.ON@@D.PREFIX.MIDDLE@@P.PREFIX.OUTER@í^H         Outer-Subjects;
@P.CONATIVE.ON@@R.PREFIX.MIDDLE@@P.PREFIX.OUTER@^H          Outer-Subjects;
@P.CONATIVE.OFF@                                            Outer-Subjects;


LEXICON Outer-Subjects

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@                     Filter-SBJ-DO;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@                     Filter-SBJ-DO;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                     Filter-SBJ-DO;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@                     Filter-SBJ-DO;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@                     Filter-SBJ-DO;

@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@                     Outer-Subjects-3PL;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@                     Outer-Subjects-4SG;

LEXICON Outer-Subjects-3PL
@R.GI@                                                      Filter-SBJ-DO;
@D.GI@@P.GI.ON@gi                               Outer-Subjects-3PL-WithGi;

LEXICON Outer-Subjects-3PL-WithGi

! We need three paths to produce gi- here:
!
!   1. If this verb contains a conative prefix, then we don't fiddle with
!      any of the flag diacritics (since the conative already sets these
!      to PREFIX.OUTER);
!
!   2. If there isn't a conative prefix and gi- is preceded by another middle
!      prefix (e.g., a direct object  marker of some kind), then gi- should
!      be treated as an inner prefix, instead;
!
!   3. Likewise, if there isn't a conative prefix and gi- isn't preceded by
!      a middle prefix, then we should treat is as an inner prefix, like
!      usual.
!
! Case (2) is really only necessary because of the behaviour of tone marking
! in si-PFV paradigms.  If gi- is preceded by gu- or another direct object
! marker, then gi- appears with low tone (e.g., tágu_gìs_chíízh "they painted
! it (a place or space)").  If there aren't any other preceding middle
! prefixes, then gi- appears with mid tone (e.g., sinì tádà_gis_chíízh "each
! and every one of them painted my face"; see Goddard-20210622), as it does
! in stative si-IPFV paradigms, as well.
@R.CONATIVE.ON@                                             Filter-SBJ-DO;
@R.CONATIVE.OFF@@D.PREFIX.MIDDLE@@P.PREFIX.MIDDLE@          Filter-SBJ-DO;
@R.CONATIVE.OFF@@R.PREFIX.MIDDLE@@P.PREFIX.INNER@           Filter-SBJ-DO;

LEXICON Outer-Subjects-4SG
! Same, but for ts'i-. (This would be much simpler if we treated inflectional
! allomorphy as a phonologically conditioned alternation between full and
! reduced/contracted froms, with tone-bearing prefixes preventing contraction.
! If that approach worked, then the presence of high-tone í- would result in 
! full TAMA chunks being selected, without needing to monkey around with
! prefix flags like this...)
@R.CONATIVE.ON@ts'i                                         Filter-SBJ-DO;
@R.CONATIVE.OFF@@D.PREFIX.MIDDLE@@P.PREFIX.MIDDLE@ts'i      Filter-SBJ-DO;
@R.CONATIVE.OFF@@R.PREFIX.MIDDLE@@P.PREFIX.INNER@ts'i       Filter-SBJ-DO;


LEXICON Filter-SBJ-DO

! Only provide paths out of this lexicon for transitive forms whose subject-
! object combinations are semantically plausible.

! No 1SG > 1SG ("I saw me") or 1SG > 1PL ("I saw us").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.4@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTPERSON.AREAL@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTPERSON.INDEF@
    Filter-SBJ-IO;

! No 2SG > 2SG ("you saw you") or 2SG > 2PL ("you saw you guys").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.4@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTPERSON.AREAL@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTPERSON.INDEF@
    Filter-SBJ-IO;

! No 1PL > 1SG ("we saw me") or 1PL > 1PL ("we saw us").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.4@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTPERSON.AREAL@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTPERSON.INDEF@
    Filter-SBJ-IO;

! No 2PL > 2SG ("you guys saw you") or 2PL > 2PL ("you guys saw you guys").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.4@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTPERSON.AREAL@
    Filter-SBJ-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTPERSON.INDEF@
    Filter-SBJ-IO;

! All 3SG/3PL/4 forms are fine, since combinations of subjects and direct
! objects in these persons that have special morphological realizations
! are handled elsewhere above.
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                 Filter-SBJ-IO;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@                 Filter-SBJ-IO;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@                 Filter-SBJ-IO;

! All (non-3/4) reflexive and reciprocal forms are fine. (3SG/3PL/4 forms are
! handled above)
@U.SUBJECTPERSON.1@@U.OBJECTPERSON.REFL@                Filter-SBJ-IO;
@U.SUBJECTPERSON.2@@U.OBJECTPERSON.REFL@                Filter-SBJ-IO;
@U.SUBJECTPERSON.1@@U.OBJECTPERSON.RECIP@               Filter-SBJ-IO;
@U.SUBJECTPERSON.2@@U.OBJECTPERSON.RECIP@               Filter-SBJ-IO;


LEXICON Filter-SBJ-IO

@U.VALENCE.INTRANSITIVE@                                #;
@U.VALENCE.TRANSITIVE@                                  #;
@U.VALENCE.TRANSITIONAL@                                #;
@U.VALENCE.OBLIQUEOBJECT@                               Filter-SBJ-IO-Checks;
@U.VALENCE.DITRANSITIVE@                                Filter-SBJ-IO-Checks;

LEXICON Filter-SBJ-IO-Checks

! Only provide paths out of this lexicon for ditransitive forms whose subject-
! oblique object combinations are semantically plausible.

! No 1SG > 1SG ("I saw me") or 1SG > 1PL ("I saw us").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.2@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.2@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBLIQUEPERSON.AREAL@
    Filter-DO-IO;

! No 2SG > 2SG ("you saw you") or 2SG > 2PL ("you saw you guys").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.1@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.1@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBLIQUEPERSON.AREAL@
    Filter-DO-IO;

! No 1PL > 1SG ("we saw me") or 1PL > 1PL ("we saw us").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.2@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.2@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBLIQUEPERSON.AREAL@
    Filter-DO-IO;

! No 2PL > 2SG ("you guys saw you") or 2PL > 2PL ("you guys saw you guys").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.1@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.1@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@
    Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBLIQUEPERSON.AREAL@
    Filter-DO-IO;

! All 3SG/3PL/4 forms are fine, since combinations of subjects and oblique
! objects in these persons that have special morphological realizations are
! handled elsewhere above.
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@                 Filter-DO-IO;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@                 Filter-DO-IO;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@                 Filter-DO-IO;

! All (non-3/4) reflexive and reciprocal forms are fine at this point.
@U.SUBJECTPERSON.1@@U.OBLIQUEPERSON.REFL@               Filter-DO-IO;
@U.SUBJECTPERSON.2@@U.OBLIQUEPERSON.REFL@               Filter-DO-IO;
@U.SUBJECTPERSON.1@@U.OBLIQUEPERSON.RECIP@              Filter-DO-IO;
@U.SUBJECTPERSON.2@@U.OBLIQUEPERSON.RECIP@              Filter-DO-IO;


LEXICON Filter-DO-IO

@U.VALENCE.OBLIQUEOBJECT@                               #;
@U.VALENCE.DITRANSITIVE@                                Filter-DO-IO-Checks;

LEXICON Filter-DO-IO-Checks

! Only provide paths out of this lexicon for ditransitive forms whose direct
! object-oblique object combinations are semantically plausible.

! No DOBJ.1SG > IOBJ.1SG ("gave me to me") or DOBJ.1SG > IOBJ.1PL ("gave me
! to us").
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.2@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.2@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@ #;

! No DOBJ.2SG > IOBJ.2SG ("gave you to you") or DOBJ.2SG > IOBJ.2PL ("gave
! you to you guys").
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.1@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.1@ #;
@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@ #;

! No DOBJ.1PL > IOBJ.1SG ("gave us to me") or DOBJ.1PL > IOBJ.1PL ("gave us
! to us").
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.2@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.2@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@ #;

! No DOBJ.2PL > IOBJ.2SG ("gave you guys to you") or DOBJ.2PL > IOBJ.2PL
! ("gave you guys to you guys").
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.1@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.3@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.SG@@U.OBLIQUEPERSON.4@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.1@ #;
@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@@U.OBLIQUENUMBER.PL@@U.OBLIQUEPERSON.3@ #;

! All 3SG/3PL/4 forms are fine, since combinations of subjects and direct
! objects in these persons that have special morphological realizations
! are handled elsewhere above.
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.SG@                       #;
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.PL@                       #;
@U.OBJECTPERSON.4@@U.OBJECTNUMBER.SG@                       #;

! Indefinite direct object forms (e.g., "give *something* to IO") are fine
! throughout.
@U.OBJECTPERSON.INDEF@                                      #;

! Make sure that at most one reflexive or reciprocal form appears as a direct
! object or indirect object (i.e., no DObj.REFL/RECIP-IObj.REFL/RECIP
! combinations)
@U.OBJECTPERSON.REFL@@U.OBLIQUEPERSON.1@                    #;
@U.OBJECTPERSON.REFL@@U.OBLIQUEPERSON.2@                    #;
@U.OBJECTPERSON.REFL@@U.OBLIQUEPERSON.3@                    #;
@U.OBJECTPERSON.REFL@@U.OBLIQUEPERSON.4@                    #;
@U.OBLIQUEPERSON.REFL@@U.OBJECTPERSON.1@                    #;
@U.OBLIQUEPERSON.REFL@@U.OBJECTPERSON.2@                    #;
@U.OBLIQUEPERSON.REFL@@U.OBJECTPERSON.3@                    #;
@U.OBLIQUEPERSON.REFL@@U.OBJECTPERSON.4@                    #;

@U.OBJECTPERSON.RECIP@@U.OBLIQUEPERSON.1@                   #;
@U.OBJECTPERSON.RECIP@@U.OBLIQUEPERSON.2@                   #;
@U.OBJECTPERSON.RECIP@@U.OBLIQUEPERSON.3@                   #;
@U.OBJECTPERSON.RECIP@@U.OBLIQUEPERSON.4@                   #;
@U.OBLIQUEPERSON.RECIP@@U.OBJECTPERSON.1@                   #;
@U.OBLIQUEPERSON.RECIP@@U.OBJECTPERSON.2@                   #;
@U.OBLIQUEPERSON.RECIP@@U.OBJECTPERSON.3@                   #;
@U.OBLIQUEPERSON.RECIP@@U.OBJECTPERSON.4@                   #;

! TODO: In theory, there might be contexts in which REFL-RECIP (e.g., 'they
! fed themselves to each other') and RECIP-REFL (e.g., 'they fed each other
! to themselves') might be plausible. For now, we exclude those forms from
! being produced/accepted, since they tend to be semantically odd for most
! ditransitive verbs.


! Direct object experiencer verbs have direct object inflection only
! (interpreted as 'subject' in English translations; more restricted set of
! inflectional markers than with most direct objects, since no 3>3, reflexive,
! or reciprocal marking).
LEXICON DirectSubjectAffixes

! TODO: Discuss given objects in third-person forms with Bruce, then implement 
! U.DIRECTOBJECT.GIVEN/NONE as necessary.
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.SG@si                 DirectSubjectConative;
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.SG@ni                 DirectSubjectConative;
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.SG@mi                 DirectSubjectConative;
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@nihi               DirectSubjectConative;
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@nahi               DirectSubjectConative;
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@nihi               DirectSubjectConative;
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@nahi               DirectSubjectConative;
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.PL@@P.GI.ON@gimi      DirectSubjectConative;
@U.OBJECTPERSON.4@@U.OBJECTNUMBER.SG@gu                 DirectSubjectConative;
@U.OBJECTPERSON.AREAL@@U.OBJECTNUMBER.SG@gu             DirectSubjectConative;

! Direct object experiencer verbs may also contain a conative prefix, which
! has distinctive morphophonological characteristics (see notes above).
! The presence of a conative prefix is specified in a verb's lexical entry.
LEXICON DirectSubjectConative

@P.CONATIVE.ON@@P.PREFIX.OUTER@^H                              Only3SGSubject;
@P.CONATIVE.OFF@@P.PREFIX.MIDDLE@                              Only3SGSubject;

! Impersonal and direct object experiencer verbs only have inner inflection
! for third-person singular forms.  This lexicon prevents forms with any other
! subject person-number combinations from being produced/recognized.
LEXICON Only3SGSubject

@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                                     #;
