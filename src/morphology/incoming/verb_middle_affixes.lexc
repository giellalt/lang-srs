Multichar_Symbols @D.OBJECTPERSON@ @D.PREFIX@ @D.VALENCE.TRANSITIVE@ @P.DISTRIBUTIVE.OFF@ @P.DISTRIBUTIVE.ON@ @P.PREFIX.MIDDLE@ @R.DISTRIBUTIVE.OFF@ @R.DISTRIBUTIVE.ON@ @R.OBJECTNUMBER.PL@ @R.OBJECTNUMBER.SG@ @R.OBJECTPERSON.1@ @R.OBJECTPERSON.2@ @R.OBJECTPERSON.3@ @R.OBJECTPERSON.4@ @R.OBJECTPERSON.RECIP@ @R.OBJECTPERSON.REFL@ @R.PREFIX.INNER@ @R.PREFIX.MIDDLE@ @R.PREFIX.OUTER@ @R.SUBJECTNUMBER.PL@ @R.SUBJECTNUMBER.SG@ @R.SUBJECTPERSON.3@ @R.SUBJECTPERSON.4@ @U.ASPECT.IPFV@ @U.ASPECT.PFV@ @U.ASPECT.POT@ @U.ASPECT.PROG@ @U.OBJECTNUMBER.PL@ @U.OBJECTNUMBER.SG@ @U.OBJECTPERSON.1@ @U.OBJECTPERSON.2@ @U.OBJECTPERSON.3@ @U.OBJECTPERSON.4@ @U.OBJECTPERSON.RECIP@ @U.OBJECTPERSON.REFL@ @U.PREFIX.OUTER@ @U.SUBJECTNUMBER.PL@ @U.SUBJECTNUMBER.SG@ @U.SUBJECTPERSON.1@ @U.SUBJECTPERSON.2@ @U.SUBJECTPERSON.3@ @U.SUBJECTPERSON.4@ @U.TAMA.%0@ @U.TAMA.%0i@ @U.TAMA.%0s@ @U.TAMA.i@ @U.TAMA.ii@ @U.TAMA.is@ @U.TAMA.isi@ @U.TAMA.isii@ @U.TAMA.isis@ @U.TAMA.ni@ @U.TAMA.nii@ @U.TAMA.nis@ @U.TAMA.si@ @D.TAMA.si@ @R.TAMA.si@ @U.TAMA.sii@ @D.TAMA.sii@ @R.TAMA.sii@ @U.TAMA.sis@ @D.TAMA.sis@ @R.TAMA.sis@ @U.TAMA.yi-a@ @U.TAMA.yi-y@ @U.TAMA.yi@ @U.TAMA.yii-a@ @U.TAMA.yii@ @U.TAMA.yis-y@ @U.TAMA.yis@ @U.VALENCE.INTRANSITIVE@ @U.VALENCE.TRANSITIVE@ @U.OUTERSUBJECT.ON@ @U.DIRECTOBJECT.NOMINAL@ @U.DIRECTOBJECT.NONE@ @U.OBJECTPERSON.INDEF@ @D.SUBJECTPERSON.1@ @D.SUBJECTPERSON.2@ @D.SUBJECTPERSON.3@ @R.OUTERSUBJECT.ON@ @P.GI.ON@ @D.GI@ @R.GI@

!
! TODO:
!
!   * Figure out how best to model indefinite 3SG objects ('something, someone (unstated or unspecific)', e.g.,
!     iyistsàd "I ate (it, something)" -- are these lexically determined/restricted, or are they part of every
!     direct object paradigm?  What happens with 3SG/3PL subjects: can you still get i-, or does this have to
!     have yi-/giyi-, instead?  If you can get an i- indefinite object with a verb, does that mean you can
!     always have definite objects, as well (i.e., these are a supserset of the regular transitive verbs --
!     some verbs can also take i- objects, as well as the other usual direct object markers)
!
!       @U.OBJECTNUMBER.SG@@U.OBJECTPERSON.INDEF@@D.SUBJECTPERSON.3@i                           Outer-Subjects;


LEXICON Root

! Make sure that each form passing through this lexicon is flagged for a 
! particular subject person-number combination.  For the transitive verbs,
! we need this information to restrict certain combinations of subject
! person-number and direct object marking later on.

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@                 Transitivity;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@                 Transitivity;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                 Transitivity;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@                 Transitivity;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@                 Transitivity;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@                 Transitivity;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@                 Transitivity;


LEXICON Transitivity

@U.VALENCE.INTRANSITIVE@                                Intransitive-Markers;
@U.VALENCE.TRANSITIVE@                                  Transitive-Markers;


LEXICON Intransitive-Markers

! With intransitive verbs, the only two affixes that can appear in this region
! of the verb are the two 'outer' subject markers gi- 3PL and ts'i- 4SG.

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@                         #;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@                         #;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                         #;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@                         #;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@                         #;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@P.PREFIX.MIDDLE@gi      #;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@@P.PREFIX.MIDDLE@ts'i    #;


LEXICON Transitive-Markers

! With transitive verbs, both direct object markers and the 'outer' subject
! markers mentioned above can appear in this part of the verb -- in the case
! of third-person subjects acting on third-person objects, sometimes combining
! to have morphologically unexpected forms.
!
! Additionally, marking on the verb for third-person direct objects is not
! required in Tsuut'ina when a full nominal object is present.  We mark these
! cases with the flag U.DIRECTOBJECT.NOMINAL.

! 1SG direct object (e.g., siyíʔí "you (sg.) saw me")
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@si    Outer-Subjects;

! 2SG direct object (e.g., niyisʔí "I saw you")
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.SG@@P.PREFIX.MIDDLE@ni    Outer-Subjects;

! Handle 3SG direct objects in their own lexicon.
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.SG@                   3SG-Direct-Objects;

! 1PL direct object (e.g., nihiyíʔí / nahiyíʔí "you (sg.) saw us")
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nihi  Outer-Subjects;
@U.OBJECTPERSON.1@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nahi  Outer-Subjects;

! 2PL direct object (e.g., nihiyisʔí / nahiyisʔí "I saw you (pl.)")
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nihi  Outer-Subjects;
@U.OBJECTPERSON.2@@U.OBJECTNUMBER.PL@@P.PREFIX.MIDDLE@nahi  Outer-Subjects;

! Handle 3PL direct objects in their own lexicon.
@U.OBJECTPERSON.3@@U.OBJECTNUMBER.PL@                   3PL-Direct-Objects;

! Handle reflexives in their own lexicon.
@U.OBJECTPERSON.REFL@@P.PREFIX.MIDDLE@            Reflexive-Direct-Objects;

! Handle reciprocals in their own lexicon.
@U.OBJECTPERSON.RECIP@@P.PREFIX.MIDDLE@          Reciprocal-Direct-Objects;


LEXICON 3SG-Direct-Objects

! 3SG direct object with non-3 subject (e.g., ístłí yisʔí "I saw a
! horse", miyisʔí "I saw it")
@D.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                                         Outer-Subjects;
@D.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@P.PREFIX.MIDDLE@mi                         Outer-Subjects;

! 3SG direct object with 3SG subject (e.g., ístłí yíʔí "s/he saw a
! horse", yáʔí "s/he saw it (< yi- + yíʔí))
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                     Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@P.PREFIX.MIDDLE@yi     Filter-Transitives;

! 3SG direct object with 3PL subject (e.g., ístłí gáʔí "they saw a
! horse" (< gi- + yíʔí), giyáʔí "they saw it (< giyi- + yíʔí))
!
! Since gi- could already have appeared in front of a prefix to
! the left of here, we need two separate paths here: one for
! when this is the first occurrence of gi- (@D.GI@, followed by
! @P.GI.ON@), and another for the case when gi- has already been
! encountered/expressed (@R.GI@).
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                     Outer-Subjects;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@R.GI@@P.PREFIX.MIDDLE@yi   Filter-Transitives;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@giyi   Filter-Transitives;


LEXICON 3PL-Direct-Objects

! 3PL direct object with non-3 subject (e.g., gimilích'àká yisʔí "I saw their horses", gimiyisʔí "I saw them")
@D.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                                         Outer-Subjects;
@D.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@gimi        Outer-Subjects;
@D.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@R.GI@@P.PREFIX.MIDDLE@mi                   Outer-Subjects;

! 3PL direct object with 3SG subject (e.g., giyilích'àká yíʔí "s/he saw their horses", gáʔí "s/he saw them" (< gi- + ))
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                     Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@R.GI@                  Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@gi     Filter-Transitives;

! 3PL direct object with 3PL subject (e.g., gulích'àká gáʔí "they saw their horses", gugáʔí "they saw them")
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NOMINAL@                     Outer-Subjects;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.DIRECTOBJECT.NONE@@P.PREFIX.MIDDLE@gu     Outer-Subjects;


LEXICON Reflexive-Direct-Objects

! Note 3PL igidi-, 4SG its'idi-
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@idi                  Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@idi                  Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.3@@U.OBJECTNUMBER.SG@idi                  Filter-Transitives;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@idi                  Filter-Transitives;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@idi                  Filter-Transitives;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.OBJECTNUMBER.PL@@R.GI@idi            Filter-Transitives;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@@U.OBJECTNUMBER.PL@@D.GI@@P.GI.ON@igidi Filter-Transitives;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@@U.OBJECTNUMBER.SG@its'idi              Filter-Transitives;


LEXICON Reciprocal-Direct-Objects

! Reciprocal forms (only with plural and 4th-person subjects, e.g., atłiyasʔí "you saw each other",
! astłits'áʔí "someone saw each other")
!
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@atłi                  Filter-Transitives;
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@astłi                 Filter-Transitives;
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@atłi                  Filter-Transitives;
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@astłi                 Filter-Transitives;
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@atłi                  Outer-Subjects;
@U.OBJECTNUMBER.PL@@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.3@astłi                 Outer-Subjects;
@U.OBJECTNUMBER.SG@@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@atłi                  Outer-Subjects;
@U.OBJECTNUMBER.SG@@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.4@astłi                 Outer-Subjects;


LEXICON Outer-Subjects

@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.SG@                                         Filter-Transitives;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.SG@                                         Filter-Transitives;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                                         Filter-Transitives;
@U.SUBJECTPERSON.1@@U.SUBJECTNUMBER.PL@                                         Filter-Transitives;
@U.SUBJECTPERSON.2@@U.SUBJECTNUMBER.PL@                                         Filter-Transitives;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@R.GI@                                   Filter-Transitives;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@@D.GI@@P.GI.ON@@P.PREFIX.MIDDLE@gi       Filter-Transitives;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@@P.PREFIX.MIDDLE@ts'i                    Filter-Transitives;


LEXICON Filter-Transitives

! Only provide paths out of this lexicon for transitive forms whose
! subject-object combinations are semantically plausible.

! No 1SG > 1SG ("I saw me") or 1SG > 1PL ("I saw us").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@    #;

! No 2SG > 2SG ("you saw you") or 2SG > 2PL ("you saw you guys").
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@    #;
@U.SUBJECTNUMBER.SG@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@    #;

! No 1PL > 1SG ("we saw me") or 1PL > 1PL ("we saw us").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.2@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.2@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.1@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@    #;

! No 2PL > 2SG ("you guys saw you") or 2PL > 2PL ("you guys saw you guys").
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.1@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.SG@@U.OBJECTPERSON.3@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.1@    #;
@U.SUBJECTNUMBER.PL@@U.SUBJECTPERSON.2@@U.OBJECTNUMBER.PL@@U.OBJECTPERSON.3@    #;

! All 3SG/3PL/4 forms are fine, since combinations of subjects and direct
! objects in these persons that have special morphological realizations
! are handled elsewhere above.
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.SG@                                         #;
@U.SUBJECTPERSON.3@@U.SUBJECTNUMBER.PL@                                         #;
@U.SUBJECTPERSON.4@@U.SUBJECTNUMBER.SG@                                         #;

! All reflexive and reciprocal forms are fine.
@U.OBJECTPERSON.REFL@                                                           #;
@U.OBJECTPERSON.RECIP@                                                          #;
