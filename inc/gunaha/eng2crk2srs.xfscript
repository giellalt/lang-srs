load inc/english-phrase-analysis.fomabin
define eng2crk

define crkSubjectTags [ "+0Sg" | "+1Sg" | "+2Sg" | "+3Sg" | "+4Sg" | "+1Pl" | "+21Pl" | "+2Pl" | "+3Pl" | "+4Pl" | "+4Sg/Pl" | "+5Sg/Pl" | "+X" ] ;

define crkObjectTags [ "+0SgO" | "+1SgO" | "+2SgO" | "+3SgO" | "+4SgO" | "+1PlO" | "+21PlO" | "+2PlO" | "+3PlO" | "+4PlO" | "+4Sg/PlO" | "+5Sg/PlO" | "+XO" ] ;

define srsSubjectTags [ "+SbjSg1" | "+SbjSg2" | "+SbjSg3" | "+SbjSg4" | "+SbjPl1" | "+SbjPl2" | "+SbjPl3" ] ;

define srsObjectTags [ "+DObjSg1" | "+DObjSg2" | "+DObjSg3" | "+DObjSg4" | "+DObjPl1" | "+DObjPl2" | "+DObjPl3" ] ;

define srsIObjectTags [ "+IObjSg3" | "+IObjPl3" ] ;

define crkVerbClasses [ "+II" | "+AI" | "+TI" | "+TA" ] ;

regex [ [ "+TI" | "+TA" ] -> "+D" || _ ?* crkSubjectTags crkObjectTags srsIObjectTags ( "+Neg" ) ] ;
define DitransitiveTag

regex [ [ "+TI" | "+TA" ] -> "+T" || _ ?* crkSubjectTags [ crkObjectTags | srsIObjectTags ] ( "+Neg" ) .#. ] ;
define TransitiveTag

regex [ [ "+AI" | "+II" ] -> "+I" || _ ?* crkSubjectTags ( "+Neg" ) .#. ] ;
define IntransitiveTag

define ValencyTag [ DitransitiveTag .o. TransitiveTag .o. IntransitiveTag ] ;

regex [ "+Prog" -> 0 || _ ?* "+Rept" ?* .#. ] ;
define RepetitiveTag

regex [ [ "+Def" | "+Fut" | "+Int" | "+Cond" | "+Inf" ] -> "+Ipfv" || _ \[ "+Prog" ]* .#. ] ;
define ImperfectiveTag

regex [ [ "+Prt" ] -> "+Pfv" || _ \[ "+Prog" ]* .#. ] ;
define PerfectiveTag

regex [ [ "+Prs" | "+Prt" | "+Fut" | "+Int" | "+Cond" | "+Inf" ] -> 0 || _ ?* "+Prog" ] ;
define ProgressiveTag

regex [ [..] -> "+Ipfv" || [ "+D" | "+T" | "+I" ] _ \[ "+Ipfv" | "+Pfv" | "+Prog" ] ] ;
define DefaultAspectTag

define AspectTag [ RepetitiveTag .o. ImperfectiveTag .o. PerfectiveTag .o. ProgressiveTag .o. DefaultAspectTag ] ;

regex [ "+1Sg" -> "+SbjSg1"
,, "+2Sg" -> "+SbjSg2"
,, "+3Sg" -> "+SbjSg3"
,, "+X" -> "+SbjSg4"
,, [ "+1Pl" | "+21Pl" ] -> "+SbjPl1"
,, "+2Pl" -> "+SbjPl2"
,, [ "+3Pl" | "+4Sg/Pl" | "+5Sg/Pl" ] -> "+SbjPl3"
] ;
define SubjectConversion

regex [ "+1SgO" -> "+DObjSg1"
,, "+2SgO" -> "+DObjSg2"
,, [ "+0SgO" | "+3SgO" ] -> "+DObjSg3"
,, "+XO" -> "+DObjSg4"
,, [ "+1PlO" | "+21PlO" ] -> "+DObjPl1"
,, "+2PlO" -> "+SbjPl2"
,, [ "+3PlO" | "+4Sg/PlO" | "+5Sg/Pl" ] -> "+DObjPl3"
] ;
define ObjectConversion

regex [ [ {to him} | {to her} ] -> {IObjSg} ,, {to them} -> {IObjPl} ] ;
define crkIObj

regex [ [..] -> "+IObjSg3" || {IObjSg} ?* _ ( "+Neg" ) .#. ]
.o.  [ [..] -> "+IObjPl3" || {IObjPl} ?* _ ( "+Neg" ) .#. ]
.o. [ [ {IObjSg} | {IObjPl} ] -> 0 ]
;
define srsIObj

regex [ [..] -> "+Neg" || "+Neg" ?* _ .#. ] .o. [ "+Neg" -> 0 || _ \.#. ] ;
define finalizeNeg

regex [ "+Rept" -> "+Rep" ] ;
define crkRept2srsRep

! regex [ {IObjSg} -> "@P.IObj.Sg@" ] .o. [ {IObjPl} -> "@P.IObj.Pl@" ]
! .o. [ [..] -> "+IObjSg3" "@R.IObj.Sg@" | "@D.IObj.Sg@" || _ .#. ]
! .o. [ [..] -> "+IObjPl3" "@R.IObj.Pl@" | "@D.IObj.Pl@" || _ .#. ]
! ;
! define srsIObj

! regex [ [ {to him} | {to her} ] -> "@P.IObj.Sg@" ,, {to them} -> "@P.IObj.Pl@" ] ;
! define crkIObj
! 
! regex [ [..] -> "+IObjSg3" "@R.IObj.Sg@" | "@D.IObj.Sg@" || _ .#. ]
! .o. [ [..] -> "+IObjPl3" "@R.IObj.Pl@" | "@D.IObj.Pl@" || _ .#. ]
! ;
! define srsIObj

set flag-is-epsilon ON

regex [ crkIObj
.o. eng2crk
.o. srsIObj
.o. finalizeNeg
] ;

twosided flag-diacritics
define crk2srsIObj

set flag-is-epsilon ON

regex [ crk2srsIObj
.o. ValencyTag
.o. AspectTag
.o. SubjectConversion
.o. ObjectConversion
] ;
